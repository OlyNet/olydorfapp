apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    signingConfigs {
        release {
            if (System.getenv("RELEASE_BUILD").equals("true")) {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            } else {
                storeFile file("/dev/null")
                storePassword "dummy"
                keyAlias "dummy"
                keyPassword "dummy"
            }
        }
    }
    defaultConfig {
        applicationId "eu.olynet.olydorfapp"
        minSdkVersion 16
        targetSdkVersion 25

        /* use either the Jenkins build number as version code or a user specified one */
        if (System.getenv("BUILD_NUMBER") != null) {
            versionCode Integer.parseInt(System.getenv("BUILD_NUMBER"))
        } else {
            versionCode 14
        }

        versionName "0.9"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Enabling multidex support.
        multiDexEnabled true
        jackOptions {
            enabled true
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries = false
    }
    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/ASL2.0'
        pickFirst 'META-INF/services/javax.ws.rs.ext.Providers'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release

            /* append the Jenkins build number if present */
            if (System.getenv("BUILD_NUMBER") != null
                    && !System.getenv("BUILD_NUMBER").equals("")) {
                versionNameSuffix "." + System.getenv("BUILD_NUMBER")
            } else {
                versionNameSuffix "-MANUAL"
            }
        }
        debug {
            versionNameSuffix "-SNAPSHOT"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Multidex support library (needed for Android <5.0)
    compile 'com.android.support:multidex:1.0.1'
    // For cloud messaging!
    compile 'com.google.firebase:firebase-messaging:9.8.0'
    // For design components (nav drawer, actionbar, cards, ...)
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.android.support:cardview-v7:25.0.0'
    compile 'com.android.support:recyclerview-v7:25.0.0'
    compile 'com.takisoft.fix:preference-v7:25.0.0.0'
    compile 'com.makeramen:roundedimageview:2.2.1'
    // For QR-code scanning
    compile 'me.dm7.barcodescanner:zxing:1.8.4'
    // Came with sample :-P
    compile 'de.hdodenhof:circleimageview:1.2.2'
    compile 'com.mcxiaoke.volley:library:1.0.15'
    // License Dialog
    compile('de.psdev.licensesdialog:licensesdialog:1.8.1')
    // Apache Commons
    compile 'org.apache.commons:commons-lang3:3.4'
    // RESTEasy support
    compile 'com.fasterxml.jackson.core:jackson-core:2.7.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.3'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.7.3'
    compile 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.7.3'
    compile 'org.jboss.resteasy:resteasy-client:3.0.16.Final'
    // Caching
    compile('com.vincentbrison.openlibraries.android:dualcache:2.2.2@jar') {
        transitive = true
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    // TESTING DEPENDENCIES
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile('com.android.support.test:runner:0.3') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}
