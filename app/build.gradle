apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "eu.olynet.olydorfapp"
        minSdkVersion 16
        targetSdkVersion 23
        /* use either the Jenkins build number as version code or a user specified one */
        if (System.getenv("BUILD_NUMBER") != null && !System.getenv("BUILD_NUMBER").equals("")) {
            versionCode Integer.parseInt(System.getenv("BUILD_NUMBER"))
        } else {
            versionCode 7
        }
        /* append either the Jenkins build number or 'local' */
        versionName "0.1"

        // Enabling multidex support.
        multiDexEnabled true
    }
    signingConfigs {
        release {
            if (System.getenv("RELEASE_BUILD").equals("true")) {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            /* sign only if the correct environment variable is set */
            if (System.getenv("RELEASE_BUILD").equals("true")) {
                signingConfig signingConfigs.release
            }

            /* append the build number if present */
            if (System.getenv("BUILD_NUMBER") != null && !System.getenv("BUILD_NUMBER").equals("")) {
                versionNameSuffix "." + System.getenv("BUILD_NUMBER")
            }
        }
        debug {
            versionNameSuffix "-SNAPSHOT"
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries = true
    }
    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/ASL2.0'
        pickFirst 'META-INF/services/javax.ws.rs.ext.Providers'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    // Multidex support library (needed for Android <5.0)
    compile 'com.android.support:multidex:1.0.1'
    // For cloud messaging!
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    // For design components (nav drawer, actionbar, cards, ...)
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.android.support:design:23.2.0'
    compile 'com.android.support:cardview-v7:23.2.0'
    compile 'com.android.support:recyclerview-v7:23.2.0'
    compile 'com.makeramen:roundedimageview:2.2.1'
    // For QR-code scanning
    compile 'me.dm7.barcodescanner:zxing:1.8.4'
    // Came with sample :-P
    compile 'de.hdodenhof:circleimageview:1.2.2'
    compile 'com.mcxiaoke.volley:library:1.0.15'
    // RESTEasy support
    compile 'org.codehaus.jackson:jackson-core-asl:1.9.13'
    compile 'org.codehaus.jackson:jackson-jaxrs:1.9.13'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    compile 'org.codehaus.jackson:jackson-xc:1.9.13'
    compile 'org.jboss.resteasy:resteasy-jackson-provider:3.0.14.Final'
    compile 'org.jboss.resteasy:resteasy-client:3.0.14.Final'
    // Caching
    compile('com.vincentbrison.openlibraries.android:dualcache:2.2.2@jar') {
        transitive = true
    }
}
